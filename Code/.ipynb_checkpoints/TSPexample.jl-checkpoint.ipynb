{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "cd(\"$(homedir())/GitHubRepos/LFR_lcc/Code/JuliaLazyConstraints\")\n",
    "\n",
    "#  Copyright 2017, Iain Dunning, Joey Huchette, Miles Lubin, and contributors\n",
    "#  This Source Code Form is subject to the terms of the Mozilla Public\n",
    "#  License, v. 2.0. If a copy of the MPL was not distributed with this\n",
    "#  file, You can obtain one at http://mozilla.org/MPL/2.0/.\n",
    "#############################################################################\n",
    "# JuMP\n",
    "# An algebraic modeling langauge for Julia\n",
    "# See http://github.com/JuliaOpt/JuMP.jl\n",
    "#############################################################################\n",
    "# tsp.jl\n",
    "#\n",
    "# Solves the travelling salesman problem using integer programming and\n",
    "# lazy generation of the subtour elimination constraints.\n",
    "#############################################################################\n",
    "\n",
    "using JuMP\n",
    "using Gurobi\n",
    "using Base.Test\n",
    "\n",
    "# extractTour\n",
    "# Given a n-by-n matrix representing the solution to an undirected TSP,\n",
    "# extract the tour as a vector\n",
    "# Input:\n",
    "#  n        Number of cities\n",
    "#  sol      n-by-n 0-1 symmetric matrix representing solution\n",
    "# Output:\n",
    "#  tour     n+1 length vector of tour, starting and ending at 1\n",
    "function extractTour(n, sol)\n",
    "    tour = [1]  # Start at city 1 always\n",
    "    cur_city = 1\n",
    "    while true\n",
    "        # Look for first arc out of current city\n",
    "        for j = 1:n\n",
    "            if sol[cur_city,j] >= 1-1e-6\n",
    "                # Found next city\n",
    "                push!(tour, j)\n",
    "                # Don't ever use this arc again\n",
    "                sol[cur_city, j] = 0.0\n",
    "                sol[j, cur_city] = 0.0\n",
    "                # Move to next city\n",
    "                cur_city = j\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        # If we have come back to 1, stop\n",
    "        if cur_city == 1\n",
    "            break\n",
    "        end\n",
    "    end  # end while\n",
    "    return tour\n",
    "end\n",
    "\n",
    "# findSubtour\n",
    "# Given a n-by-n matrix representing solution to the relaxed\n",
    "# undirected TSP problem, find a set of nodes belonging to a subtour\n",
    "# Input:\n",
    "#  n        Number of cities\n",
    "#  sol      n-by-n 0-1 symmetric matrix representing solution\n",
    "# Outputs:\n",
    "#  subtour  n length vector of booleans, true iff in a particular subtour\n",
    "#  subtour_length   Number of cities in subtour (if n, no subtour found)\n",
    "function findSubtour(n, sol)\n",
    "    # Initialize to no subtour\n",
    "    subtour = fill(false,n)\n",
    "    # Always start looking at city 1\n",
    "    cur_city = 1\n",
    "    subtour[cur_city] = true\n",
    "    subtour_length = 1\n",
    "    while true\n",
    "        # Find next node that we haven't yet visited\n",
    "        found_city = false\n",
    "        for j = 1:n\n",
    "            if !subtour[j]\n",
    "                if sol[cur_city, j] >= 1 - 1e-6\n",
    "                    # Arc to unvisited city, follow it\n",
    "                    cur_city = j\n",
    "                    subtour[j] = true\n",
    "                    found_city = true\n",
    "                    subtour_length += 1\n",
    "                    break  # Move on to next city\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        if !found_city\n",
    "            # We are done\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    return subtour, subtour_length\n",
    "end\n",
    "\n",
    "# solveTSP\n",
    "# Given a matrix of city locations, solve the TSP\n",
    "# Inputs:\n",
    "#   n       Number of cities\n",
    "#   cities  n-by-2 matrix of (x,y) city locations\n",
    "# Output:\n",
    "#   path    Vector with order to cities are visited in\n",
    "function solveTSP(n, cities)\n",
    "\n",
    "    # Calculate pairwise distance matrix\n",
    "    dist = zeros(n, n)\n",
    "    for i = 1:n\n",
    "        for j = i:n\n",
    "            d = norm(cities[i,1:2] - cities[j,1:2])\n",
    "            dist[i,j] = d\n",
    "            dist[j,i] = d\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Create a model that will use Gurobi to solve\n",
    "    m = Model(solver=GurobiSolver())\n",
    "\n",
    "    # x[i,j] is 1 iff we travel between i and j, 0 otherwise\n",
    "    # Although we define all n^2 variables, we will only use\n",
    "    # the upper triangle\n",
    "    @variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "    # Minimize length of tour\n",
    "    @objective(m, Min, sum(dist[i,j]*x[i,j] for i=1:n for j=i:n))\n",
    "\n",
    "    # Make x_ij and x_ji be the same thing (undirectional)\n",
    "    # Don't allow self-arcs\n",
    "    for i = 1:n\n",
    "        @constraint(m, x[i,i] == 0)\n",
    "        for j = (i+1):n\n",
    "            @constraint(m, x[i,j] == x[j,i])\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # We must enter and leave every city once and only once\n",
    "    for i = 1:n\n",
    "        @constraint(m, sum(x[i,j] for j=1:n) == 2)\n",
    "    end\n",
    "\n",
    "    function subtour(cb)\n",
    "        # Optional: display tour starting at city 1\n",
    "        println(\"----\\nInside subtour callback\")\n",
    "        println(\"Current tour starting at city 1:\")\n",
    "        print(extractTour(n, getvalue(x)))\n",
    "\n",
    "        # Find any set of cities in a subtour\n",
    "        subtour, subtour_length = findSubtour(n, getvalue(x))\n",
    "        print(\"Found a subtour\")\n",
    "        if subtour_length == n\n",
    "            # This \"subtour\" is actually all cities, so we are done\n",
    "            println(\"Solution visits all cities\")\n",
    "            println(\"----\")\n",
    "            return\n",
    "        end\n",
    "        print(\"No segfault yet\")\n",
    "        # Subtour found - add lazy constraint\n",
    "        # We will build it up piece-by-piece\n",
    "        arcs_from_subtour = zero(AffExpr)\n",
    "\n",
    "        for i = 1:n\n",
    "            if !subtour[i]\n",
    "                # If this city isn't in subtour, skip it\n",
    "                continue\n",
    "            end\n",
    "            # Want to include all arcs from this city, which is in\n",
    "            # the subtour, to all cities not in the subtour\n",
    "            for j = 1:n\n",
    "                if i == j\n",
    "                    # Self-arc\n",
    "                    continue\n",
    "                elseif subtour[j]\n",
    "                    # Both ends in same subtour\n",
    "                    continue\n",
    "                else\n",
    "                    # j isn't in subtour\n",
    "                    arcs_from_subtour += x[i,j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # Add the new subtour elimination constraint we built\n",
    "        println(\"Adding subtour elimination cut\")\n",
    "        println(\"----\")\n",
    "        @lazyconstraint(cb, arcs_from_subtour >= 2)\n",
    "    end  # End function subtour\n",
    "\n",
    "    # Solve the problem with our cut generator\n",
    "    addlazycallback(m, subtour)\n",
    "    solve(m)\n",
    "\n",
    "    # Return best tour\n",
    "    return extractTour(n, getvalue(x))\n",
    "end  # end solveTSP\n",
    "\n",
    "\n",
    "# Create a simple instance that looks like\n",
    "#       +           +\n",
    "#   +                   +\n",
    "#       +           +\n",
    "# The optimal tour is obvious, but the initial solution will be\n",
    "#    /--+           +--\\\n",
    "#   +               |   +\n",
    "#    \\--+           +--/\n",
    "n = 6\n",
    "cities = [ 50 200;\n",
    "                    100 100;\n",
    "                    100 300;\n",
    "                    500 100;\n",
    "                    500 300;\n",
    "                    550 200]\n",
    "tour = solveTSP(n, cities)\n",
    "println(\"Solution: \")\n",
    "println(tour)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.5.2",
   "language": "julia",
   "name": "julia-0.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
